terraform {
  required_version = ">=0.11.0"
}

provider "aws" {
  region  = "ap-southeast-1"
}

locals {
  name = "HelloSDK"
}

data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["099720109477"] # Canonical
}

resource "aws_instance" "web" {
  ami           = data.aws_ami.ubuntu.id
  instance_type = "t3.medium"
  iam_instance_profile = aws_iam_instance_profile.main.name
  # key_name = "your_key"

  tags = {
    Name = "HelloSDK"
  }
}

resource "aws_iam_instance_profile" "main" {
  name = "${local.name}Profile"
  path = "/"
  role = aws_iam_role.main.name
}

resource "aws_iam_role" "main" {
  name               = "${local.name}Role"
  path               = "/"
  description        = "Role assigned to the ${local.name}Profile instance profile (generated by Terraform)"
  assume_role_policy = data.aws_iam_policy_document.assume_role.json
}

data "aws_iam_policy_document" "assume_role" {
  statement {
    effect = "Allow"

    principals {
      type        = "Service"
      identifiers = ["ec2.amazonaws.com"]
    }

    actions = ["sts:AssumeRole"]
  }
}

resource "aws_iam_role_policy_attachment" "ssm_policy" {
  role       = aws_iam_role.main.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
}